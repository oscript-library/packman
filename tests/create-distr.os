#Использовать asserts
#Использовать tempfiles
#Использовать "../src"

Перем Лог;

Функция ПолучитьСписокТестов(Тестирование) Экспорт
    
    Список = Новый Массив;
    Список.Добавить("Тест_ДолженПроверитьЧтоПараметрыКомСтрокиПолученияВерсииРазобраны");
    Список.Добавить("Тест_ДолженПроверитьЧтоВерсияПолученаИзХранилища");
    Список.Добавить("Тест_ДолженПроверитьЧтоСозданыФайлыКонфигурацииПоставщика");
    Список.Добавить("Тест_ДолженПроверитьЧтоСозданДистрибутив");
    Список.Добавить("Тест_ДолженПроверитьЧтоСозданАрхивДистрибутива");
    Список.Добавить("Тест_ДолженПроверитьЧтоВерсияПолученаСПомощьюTool1CD");
    Список.Добавить("Тест_ДолженПроверитьЧтоВерсияПолученаСПомощьюTool1CDИЕстьФайлПараметров");
    Список.Добавить("Тест_ДолженПроверитьЧтоКаталогСборкиОчищается");
    
    Возврат Список;
    
КонецФункции

Процедура ПослеЗапускаТеста() Экспорт
    ВременныеФайлы.Удалить();
КонецПроцедуры

Процедура Тест_ДолженПроверитьЧтоПараметрыКомСтрокиПолученияВерсииРазобраны() Экспорт
    
    ПараметрыКомСтроки = Новый Соответствие;
    ПараметрыКомСтроки["АдресХранилища"] = "/some/path/storage";
    ПараметрыКомСтроки["-storage-user"] = "Администратор";
    ПараметрыКомСтроки["-storage-pwd"]  = "123";
    ПараметрыКомСтроки["-storage-v"]    = "4";
    
    Команда = Новый КомандаВыгрузитьИзХранилища;
    
    Параметры = Команда.РазобратьПараметры(ПараметрыКомСтроки);
    
    Ожидаем.Что(Параметры.АдресХранилища).Равно("/some/path/storage");
    Ожидаем.Что(Параметры.ПользовательХранилища).Равно("Администратор");
    Ожидаем.Что(Параметры.ПарольХранилища).Равно("123");
    Ожидаем.Что(Параметры.ВерсияХранилища).Равно("4");
    
КонецПроцедуры

Процедура Тест_ДолженПроверитьЧтоВерсияПолученаИзХранилища() Экспорт
    
    Команда = Новый КомандаВыгрузитьИзХранилища();
    
    ВремКаталогХранилища = СоздатьВременноеТестовоеХранилище();
    ВремФайлКонфигурации = ПолучитьИмяВременногоФайла("cf");
    Попытка
        Команда.ВыгрузитьВерсиюИзХранилища(ВремКаталогХранилища, 2, ВремФайлКонфигурации, "Администратор");
        ФайлТест = Новый Файл(ВремФайлКонфигурации);
		Ожидаем.Что(ФайлТест.Существует(), "файл конфигурации должен существовать").ЕстьИстина(); 
    Исключение
        УдалитьФайлы(ВремКаталогХранилища);
        УдалитьФайлы(ВремФайлКонфигурации);
        ВызватьИсключение;
    КонецПопытки;
    
    УдалитьФайлы(ВремКаталогХранилища);
    УдалитьФайлы(ВремФайлКонфигурации);
    
КонецПроцедуры // Тест_ДолженПроверитьЧтоВерсияПолученаИзХранилища()

Процедура Тест_ДолженПроверитьЧтоВерсияПолученаСПомощьюTool1CD() Экспорт
	
    Команда = Новый КомандаВыгрузитьИзХранилища();
    
    ВремКаталогХранилища = СоздатьВременноеТестовоеХранилище();
    ВремФайлКонфигурации = ВременныеФайлы.НовоеИмяФайла("cf");
    
    Команда.ВыгрузитьВерсиюСредствамиTool1CD(ВремКаталогХранилища, 2, ВремФайлКонфигурации);
    ФайлТест = Новый Файл(ВремФайлКонфигурации);
    Ожидаем.Что(ФайлТест.Существует(), "файл конфигурации должен существовать").ЕстьИстина(); 
    
КонецПроцедуры

Процедура Тест_ДолженПроверитьЧтоВерсияПолученаСПомощьюTool1CDИЕстьФайлПараметров() Экспорт
	
    Команда = Новый КомандаВыгрузитьИзХранилища();
    
    ВремКаталогХранилища = СоздатьВременноеТестовоеХранилище();
    ВремФайлКонфигурации = ВременныеФайлы.НовоеИмяФайла("cf");
	ФайлПараметровКоммита = ВременныеФайлы.НовоеИмяФайла("txt");
    
    Команда.ВыгрузитьВерсиюСредствамиTool1CD(ВремКаталогХранилища, 2, ВремФайлКонфигурации, ФайлПараметровКоммита);
    ФайлТест = Новый Файл(ВремФайлКонфигурации);
    Ожидаем.Что(ФайлТест.Существует(), "файл конфигурации должен существовать").ЕстьИстина(); 

	ФайлТест = Новый Файл(ФайлПараметровКоммита);
	Ожидаем.Что(ФайлТест.Существует(), "файл параметров коммита должен существовать").ЕстьИстина(); 

	ПараметрыКоммита = Команда.ПрочитатьПараметрыКоммита(ФайлТест.ПолноеИмя);
	Ожидаем.Что(ПараметрыКоммита).ИмеетТип("Структура");
	Ожидаем.Что(ПараметрыКоммита.НомерВерсииХранилища).Равно(2);
    
КонецПроцедуры

Функция СоздатьВременноеТестовоеХранилище() Экспорт
    ФайлХранилища = ТекущийСценарий().Каталог + "/fixtures/storage.1CD";
    ВремКаталогХранилища = ВременныеФайлы.СоздатьКаталог();
    СоздатьКаталог(ВремКаталогХранилища);
    КопироватьФайл(ФайлХранилища, ВремКаталогХранилища + "/1cv8ddb.1CD");
    
    Возврат ВремКаталогХранилища;
КонецФункции

Процедура Тест_ДолженПроверитьЧтоСозданыФайлыКонфигурацииПоставщика() Экспорт
    
    ВременноеХранилище = СоздатьВременноеТестовоеХранилище();
    КаталогСборки = ВременныеФайлы.СоздатьКаталог();
    Параметры = Новый Структура;
    Параметры.Вставить("АдресХранилища", ВременноеХранилище);
    Параметры.Вставить("КаталогСборки", КаталогСборки);
    Параметры.Вставить("ПользовательХранилища","Администратор");
    Параметры.Вставить("ПарольХранилища", "");
    Параметры.Вставить("ВерсияХранилища", "18");
    Параметры.Вставить("КаталогВерсий", ТекущийКаталог()+"__-__");
    Параметры.Вставить("ПредыдущиеВерсии", Новый Массив);
    
    Команда = Новый КомандаВыгрузитьИзХранилища;
    УправлениеКонфигуратором = Новый УправлениеКонфигуратором;
    УправлениеКонфигуратором.КаталогСборки(КаталогСборки);
    
    ВерсияИзХранилища = ПолучитьИмяВременногоФайла("cf");
    Команда.ВыгрузитьВерсиюИзХранилища(Параметры.АдресХранилища, 18, ВерсияИзХранилища, "Администратор");
    Команда.ЗагрузитьКонфигурациюВБазуСборки(УправлениеКонфигуратором, ВерсияИзХранилища);
    
    Команда = Новый КомандаСоздатьФайлыПоставки;
    Команда.СоздатьФайлыКонфигурацииПоставщика(УправлениеКонфигуратором, Параметры.КаталогВерсий, Параметры.ПредыдущиеВерсии);
    
    ФайлКонфигурации = Новый Файл(ОбъединитьПути(КаталогСборки, "1Cv8.cf"));
    Ожидаем.Что(ФайлКонфигурации.Существует(), "Файл конфигурации должен существовать").ЕстьИстина();
    
КонецПроцедуры

Функция ТестовыйМанифест()
    Возврат ТекущийСценарий().Каталог + "/fixtures/package.edf";
КонецФункции

Функция СоздатьТестовыйДистрибутив()
	
    КаталогСборки = ВременныеФайлы.СоздатьКаталог();
    
    Команда = Новый КомандаВыгрузитьИзХранилища;
    УправлениеКонфигуратором = Новый УправлениеКонфигуратором;
    УправлениеКонфигуратором.КаталогСборки(КаталогСборки);
    
    ВерсияИзХранилища = ПолучитьИмяВременногоФайла("cf");
    Команда.ВыгрузитьВерсиюИзХранилища(СоздатьВременноеТестовоеХранилище(), 18, ВерсияИзХранилища, "Администратор");
    Команда.ЗагрузитьКонфигурациюВБазуСборки(УправлениеКонфигуратором, ВерсияИзХранилища);
    
    КомандаФайлыПоставки = Новый КомандаСоздатьФайлыПоставки;
    КомандаФайлыПоставки.СоздатьФайлыКонфигурацииПоставщика(УправлениеКонфигуратором, Неопределено, Новый Массив);
    
    КомандаДистрибутив = Новый КомандаСоздатьДистрибутив;
    КомандаДистрибутив.ВыполнитьСборку(УправлениеКонфигуратором, ТестовыйМанифест(), Истина, Ложь, Неопределено, Неопределено);
    
    КаталогДистр = ОбъединитьПути(УправлениеКонфигуратором.КаталогСборки(), ОкружениеСборки.ИмяКаталогаФормированияДистрибутива());

    Возврат КаталогДистр;

КонецФункции

Процедура Тест_ДолженПроверитьЧтоСозданДистрибутив() Экспорт
    
    КаталогДистр = СоздатьТестовыйДистрибутив();
    Ожидаем.Что(Новый Файл(КаталогДистр).Существует(),"Каталог дистрибутива:" + КаталогДистр).ЕстьИстина();
	Ожидаем.Что(Новый Файл(КаталогДистр+"/setup.exe").Существует(), ""+КаталогДистр + "/setup.exe").ЕстьИстина();
    
КонецПроцедуры

Процедура Тест_ДолженПроверитьЧтоСозданАрхивДистрибутива() Экспорт
	
	КаталогДистр = СоздатьТестовыйДистрибутив();
    КомандаАрхиватора = Новый КомандаАрхивироватьДистрибутив();
    
    ПараметрыКоманды = Новый Соответствие;
    ПараметрыКоманды["-in"] = КаталогДистр;
    ПараметрыКоманды["-name-prefix"] = "test";
    ПараметрыКоманды["-mdinfo"] = ОбъединитьПути(КаталогДистр,"..");
    ПараметрыКоманды["-out"] = ВременныеФайлы.СоздатьКаталог();

    КомандаАрхиватора.ВыполнитьКоманду(ПараметрыКоманды);

    ФайлыАрхива = НайтиФайлы(ПараметрыКоманды["-out"],"test-?.?.?.?.zip");
    Ожидаем.Что(ФайлыАрхива).ИмеетДлину(1); 

КонецПроцедуры

Процедура Тест_ДолженПроверитьЧтоКаталогСборкиОчищается() Экспорт
    
    Конфигуратор = ОкружениеСборки.ПолучитьКонфигуратор();
    Каталог = ВременныеФайлы.СоздатьКаталог();
    Конфигуратор.КаталогСборки(Каталог);
    Конфигуратор.СоздатьФайловуюБазу(Конфигуратор.ПутьКВременнойБазе());
    ФайлБазы = Новый Файл(ОбъединитьПути(Конфигуратор.ПутьКВременнойБазе(), "1Cv8.1CD"));
    Ожидаем.Что(ФайлБазы.Существует()).ЕстьИстина();

    КомандаОчистить = Новый КомандаОчиститьКаталог();
    КомандаОчистить.ВыполнитьОчистку(Конфигуратор);

    Ожидаем.Что(ФайлБазы.Существует()).ЕстьЛожь();

КонецПроцедуры // ИмяПроцедуры()

Лог = Логирование.ПолучитьЛог(ПараметрыСистемы.ИмяЛогаСистемы());
Лог.УстановитьУровень(УровниЛога.Отладка);